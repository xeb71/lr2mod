### Takes the old events that used to be Alexia specific and breaks them out into its own role.

init 10 python:
    def model_ad_bf_recognise_requirement():
        company_model = mc.business.company_model
        if not company_model:
            return False
        trigger_day = company_model.event_triggers_dict.get("model_ad_bf_recognise", None)
        if trigger_day is None:
            return False
        return day >= trigger_day

    model_ad_bf_recognise_action = ActionMod(
        "Boyfriend Recognises Model Ad",
        model_ad_bf_recognise_requirement,
        "model_ad_bf_recognise_label",
        menu_tooltip = "Her boyfriend has seen the company ad and recognises her.",
        category = "Business",
        is_crisis = False,
        is_mandatory_crisis = True,
    )

label fire_model_label(the_person):
    mc.name "I'm sorry [the_person.title], but I will no longer be needing you to star in our ad campaigns."
    $ the_person.change_happiness(-5)
    the_person "Oh... Okay."
    $ mc.business.fire_company_model()
    return

label company_model_general_hire_label():
    $ the_person = mc.business.company_model
    if not the_person.is_at(m_division):
        "You call [the_person.title] to the marketing office. After a minute, she appears in the door."
        $ the_person.draw_person()
        the_person "You wanted to see me?"
        mc.name "Yes. Sit down."
    else:
        $ the_person.draw_person()
        mc.name "[the_person.title] could you sit with me for a moment?"
        the_person "Sure, [the_person.mc_title]."

    $ the_person.draw_person(position = "sitting")
    mc.name "You would be the perfect person to be the new face of [mc.business.name]."
    the_person "Sir?"
    mc.name "You are smart and pretty, with a wonderful smile, so I want all our ads to feature you."
    the_person "Oh, wow, that sounds great."
    mc.name "Good. Here, this is company camera, keep it at your desk. I will let you know when we start shooting."
    the_person "Thank you, Sir. Will that be all?"
    mc.name "Yes, thank you [the_person.title]."
    $ the_person.draw_person(position = "walking_away")
    "[the_person.possessive_title!c] is now your company model. Talk to her if you want to shoot some pictures for ad campaigns!"
    $ clear_scene()
    return


label model_photography_list_label(the_person):
    mc.name "I want you to put together a new company ad. We'll need some promotional pictures to go with it."
    the_person "Sounds like a good idea to me. I've got the camera right here."
    menu:
        "Add a dose of serum to [the_person.title]'s drink" if mc.inventory.has_serum:
            "You notice [the_person.title] has a drink sitting on her desk. You quickly move to add something to it while her back is turned."
            call give_serum(the_person) from _call_give_serum_model_photo_shoot
            if _return:
                "[the_person.title] reaches for her drink without noticing anything unusual."
                the_person "Mmm. Anyway, let's get started—where do you want to do this?"

        "Add a dose of serum to [the_person.title]'s drink\n{menu_red}Requires: Serum{/menu_red} (disabled)" if not mc.inventory.has_serum:
            pass

        "Leave her drink alone":
            pass
    if the_person.effective_sluttiness("vaginal_sex") >= 65:
        $ show_popup_hint("Full Scene")
    else:
        $ show_popup_hint("Partial Scene\nModel Sluttiness")
    "[the_person.title] grabs the camera from her desk and hands it to you."

    if the_person.is_wearing_uniform: #Check to see if she should have a uniform on.
        if the_person.judge_outfit(the_person.outfit, the_person.opinion.skimpy_uniforms*5):
            the_person "Is my uniform fine for the shoot, or should I put something else on?"

        else:
            the_person "Do I get to change into something more reasonable, or do you want me in my uniform?"
    else:
        the_person "How do I look? Do you think I should wear something else for this?"
        $ the_person.draw_person(position = "back_peek")
        $ mc.change_locked_clarity(5)
        "She gives you a quick spin."
        the_person "I want to make sure I show my best side for the business."

    menu:
        "Your outfit is fine":
            mc.name "You look great already, I don't think you need to change a thing."
            $ the_person.discover_opinion("skimpy uniforms")
            $ the_person.change_slut(the_person.opinion.skimpy_uniforms, 20)
            the_person "Okay, I think I'm ready to go then!"
            $ the_outfit = the_person.current_planned_outfit

        "Put something else on for me":
            mc.name "I think you could use something with a little more pop."
            if the_person.effective_sluttiness() < 20 and the_person.has_significant_other:
                the_person "Nothing too crazy though, okay? I don't want my [the_person.so_title] to freak out when he hears about this."
            else:
                the_person "Sex sells, right, so it should be something skimpy. Did you have something in mind?"
                "She seems excited to see what you have in mind."

            call outfit_master_manager(slut_limit = the_person.sluttiness, show_overwear = False, show_underwear = False, start_mannequin = the_person) from _call_outfit_master_manager_7
            if isinstance(_return, Outfit):
                $ the_outfit = _return
                if the_person.judge_outfit(_return):
                    the_person "Yeah, I think that would look good. I'll go put that on."

                $ clear_scene()
                "[the_person.possessive_title!c] leaves to get changed and is back in a moment."
                $ the_person.apply_outfit(the_outfit, update_taboo = True)
                $ the_person.draw_person()

            else:
                mc.name "On second thought, I think you look perfect in that."
                $ the_outfit = the_person.current_planned_outfit

    $ mc.change_location(storage_room)
    "You lead [the_person.possessive_title] to a supply room. She stands against a blank wall while you get the camera ready."
    mc.name "Okay, strike a pose for me."
    $ the_person.draw_person(position = "stand4", emotion = "happy")
    "She smiles at the camera and poses for you."
    the_person "Tell me what you want me to do."

    #Outfit checks that let us be sure a girl isn't already naked before asking her to strip.
    $ outfit_state = 0 #0 = relatively normal outfit. 1 = just underwear, can't be stripped down further without being naked. 2 = already naked.
    if the_person.bra_covered or the_person.panties_covered: #She has underwear on and something over both.
        $ outfit_state = 0 #She's wearing enough we can have a "strip to your underwear" scene.
    elif the_person.wearing_bra or the_person.wearing_panties:
        $ outfit_state = 1 #She's wearing enough that we can have a strip scene.
    else:
        $ outfit_state = 2 #She's practically naked with no clothing on.
    $ slut_willingness = the_person.effective_sluttiness()
    if the_person.obedience > 120:
        $ slut_willingness += the_person.obedience - 120

    # These are "checkpoint" options for future passes through this event.
    menu:
        "Be playful":
            call photo_be_playful(the_person) from _call_photo_be_playful

        "Be flirty" if the_person.event_triggers_dict.get("camera_flirt", False) and slut_willingness + (5 * the_person.opinion.skimpy_uniforms) >= 15:
            mc.name "Be flirty for me. You're young and sexy, I want you to show that to the camera."
            call photo_be_sexy(the_person) from _call_photo_be_sexy

        "Strip to your underwear" if the_person.event_triggers_dict.get("camera_flash", False) and outfit_state == 0 and slut_willingness + (5 * the_person.opinion.skimpy_uniforms) >= 30:
            mc.name "I want to take some sexy, bold photos of you in your underwear. I want you to strip down for the camera."
            call photo_flash(the_person) from _call_photo_flash

        "Get naked" if the_person.event_triggers_dict.get("camera_naked", False) and outfit_state in (0, 1) and slut_willingness + (5 * the_person.opinion.not_wearing_anything) >= 50:
            mc.name "Strip everything off for me, I want to get some nude shots."
            call photo_naked(the_person) from _call_photo_naked

        "Touch yourself" if the_person.event_triggers_dict.get("camera_touch", False) and slut_willingness + (5 * the_person.opinion.masturbating) >= 60:
            if not outfit_state == 2:
                mc.name "Get naked and lean against that wall. I want to get some shots of you touching yourself."
                "[the_person.title] nods and starts to strip naked."
                call photo_strip_naked(the_person) from _call_photo_strip_naked
            else:
                mc.name "Lean up against that wall, I want to get some shots of you touching yourself."
            call photo_touch(the_person) from _call_photo_touch

        "Suck my dick" if the_person.event_triggers_dict.get("camera_suck", False) and slut_willingness + (5 * the_person.opinion.giving_blowjobs) >= 70:
            if not outfit_state == 2:
                mc.name "Get naked and on your knees. I want to get some close-ups of you sucking my cock."
                "[the_person.title] nods and starts to strip naked."
                call photo_strip_naked(the_person) from _call_photo_strip_naked_1
            else:
                mc.name "Come and kneel down in front of me. I want to get some close-ups of you sucking my cock."
            call photo_blowjob(the_person) from _call_photo_blowjob

        "Get fucked on camera" if the_person.event_triggers_dict.get("camera_fuck", False) and slut_willingness + (5 * the_person.opinion.vaginal_sex) >= 80:
            if not outfit_state == 2:
                mc.name "Get naked first, then I'm going to lay you down and get some pictures of you getting fucked."
            else:
                mc.name "I want you to come over here and lay down so I can take some pictures of you getting fucked."
            call photo_sex(the_person) from _call_photo_sex

        "Anal photos" if the_person.event_triggers_dict.get("camera_anal", False) and slut_willingness + (5 * the_person.opinion.anal_sex) >= 85:
            if not outfit_state == 2:
                mc.name "Get naked first, then I'm going to bend you over and get some pictures of you getting fucked in the ass."
            else:
                mc.name "I want you to bend over so I can take some pictures of you getting fucked in the ass."
            call photo_anal(the_person) from _call_photo_anal

    $ sexy_score = _return # Each scene returns the sexiness it produced (mainly based on her outfit).
    $ the_person.apply_outfit(the_outfit, show_dress_sequence = True)
    "She gets up and ready to leave."
    $ mc.change_location(m_division)
    $ the_person.draw_person(position = "sitting")
    "You hand the camera over to [the_person.title] and go back to her desk. She pulls out the memory card and puts into the computer."
    "You go through the pictures you got, discarding the poor ones and finally settling on best ones to use."
    if the_person.has_significant_other and sexy_score > 30 :
        "You wonder what her [the_person.so_title] would think about [the_person.title] showing so much skin for this ad."

    $ ad_multiplier = 1
    if sexy_score <= 10:
        $ ad_multiplier = renpy.random.randint(1, 3)
        "The photos you took of [the_person.title] are perfect for an ad placed at the back of a small medical journal."
        "Putting an ad here will boost serum value sales by {b}[ad_multiplier]%%{/b} for the next week."
    elif sexy_score <= 30:
        $ ad_multiplier = renpy.random.randint(4, 6)
        "The photos you took of [the_person.title] are perfect for an ad placed in a lifestyle magazine."
        "Putting an ad here will boost serum value sales by {b}[ad_multiplier]%%{/b} for the next week."
    elif sexy_score <= 50:
        $ ad_multiplier = renpy.random.randint(7, 10)
        "The photos you took of [the_person.title] are perfect for a sexy ad in a local tabloid."
        "Putting an ad here will boost serum value sales by {b}[ad_multiplier]%%{/b} for the next week."
    elif sexy_score <= 100:
        $ ad_multiplier = renpy.random.randint(11, 15)
        "The photos you took of [the_person.title] are perfect for a sexy ad in a soft core porn magazine."
        "Putting an ad here will boost serum value sales by {b}[ad_multiplier]%%{/b} for the next week."
    else:
        $ ad_multiplier = renpy.random.randint(16, 20)
        "The photos you took of [the_person.title] are perfect for a sexy ad in a hard core porn magazine."
        "Putting an ad here will boost serum value sales by {b}[ad_multiplier]%%{/b} for the next week."

    $ cost = int(250 * ((mc.business.research_tier + 1) ** 2) * (1.0 + (ad_multiplier / 100.0)))
    the_person "Okay, starting this campaign will cost around $[cost]. What do you think, [the_person.mc_title]? Should I prepare this ad and send it out?"
    menu:
        "Pay for the ad space\n{menu_red}Costs: $[cost]{/menu_red}" if mc.business.has_funds(cost):
            mc.name "The pictures look good, get to work and get that pushed out as soon as possible."
            the_person "You got it!"

            $ create_add_space_and_expire_action(cost, (1.0 + (ad_multiplier / 100.0)))

        "Pay for the ad space\n{menu_red}Requires: $[cost]{/menu_red} (disabled)" if not mc.business.has_funds(cost):
            pass

        "Scrap the plan":
            mc.name "I think our budget is better spent somewhere else. Sorry to put you through all that work."
            the_person "I understand. Maybe if we start selling more it'll be worth it."

    $ the_person.apply_planned_outfit()
    $ the_outfit = None

    call advance_time() from _call_advance_time_20
    return

label photo_be_playful(the_person):
    mc.name "Be playful. Give the camera a smile and just have fun with it."
    $ the_person.draw_person(position = "stand3", emotion = "happy")
    "She gives you a few more poses and seems to be enjoying herself."
    $ mc.change_locked_clarity(5)
    $ the_person.draw_person(position = "stand5", emotion = "happy")

    $ slut_willingness = the_person.effective_sluttiness() + (5*the_person.opinion.skimpy_uniforms)
    if the_person.obedience > 120:
        $ slut_willingness += the_person.obedience - 120
    menu:
        "Push her to be flirty" if slut_willingness >= 15:
            mc.name "That's great [the_person.title]. Give me a little more attitude now. You're sexy, you're young, let me feel it!"
            call photo_be_sexy(the_person) from _call_photo_be_sexy_1

        "Push her to be flirty\n{menu_red}Not slutty or obedient enough{/menu_red} (disabled)" if slut_willingness < 15:
            pass

        "Finish the shoot":
            "You take a few final pictures."
            mc.name "I think that's all we need. Good job [the_person.title], you look great."
            $ the_person.change_happiness(3)
            the_person "Glad to hear it, that was fun!"
            return the_person.outfit.outfit_slut_score
    return _return

label photo_be_sexy(the_person):
    $ the_person.event_triggers_dict["camera_flirt"] = True
    if the_person.effective_sluttiness() >= 15:
        #She's totally onboard with this idea.
        $ the_person.draw_person(position = "back_peek", emotion = "happy")
        "[the_person.possessive_title!c] spins around, peeking over her shoulder."
        the_person "Like this? Get a good shot of my butt, that's the kind of shot you probably want."
        $ mc.change_locked_clarity(10)
        "She wiggles her ass for the camera."

    else:
        #She's only doing it because you're commanding her.
        the_person "Oh my god, I feel so awkward trying to do this. This isn't me at all!"
        mc.name "Trust me, just give it a try. Turn around and shake your ass, that'll be sexy."
        $ the_person.draw_person(position = "back_peek", emotion = "happy")
        $ the_person.change_obedience(1)
        $ mc.change_locked_clarity(5)
        "She timidly wiggles her butt for the camera."

    $ slut_willingness = the_person.effective_sluttiness()
    if the_person.obedience > 120:
        $ slut_willingness += the_person.obedience - 120

    $ skimpy_uniform_bonus = (5*the_person.opinion.skimpy_uniforms)
    $ no_clothing_bonus = (5*the_person.opinion.not_wearing_anything)
    $ masturbate_bonus = (5*the_person.opinion.masturbating)

    $ outfit_state = 0 #0 = relatively normal outfit. 1 = just underwear, can't be stripped down further without being naked. 2 = already naked.
    if the_person.bra_covered or the_person.panties_covered: #She has underwear on and something over both.
        $ outfit_state = 0 #She's wearing enough we can have a "strip to your underwear" scene.
    elif the_person.wearing_bra or the_person.outfit.wearing_panties:
        $ outfit_state = 1 #She's wearing enough that we can have a strip scene.
    else:
        $ outfit_state = 2 #She's practically naked with no clothing on.
    menu:
        "Strip to your underwear" if slut_willingness+skimpy_uniform_bonus >= 30 and outfit_state == 0 and the_person.outfit.has_underwear:
            #Into her flashing the camera.
            mc.name "These are looking great. Now let's try something a little more bold. Get into your underwear for me [the_person.title]."
            call photo_flash(the_person) from _call_photo_flash_1

        "Strip to your underwear\n{menu_red}Not slutty or obedient enough{/menu_red} (disabled)" if slut_willingness+skimpy_uniform_bonus < 30 and outfit_state == 0:
            pass

        "Get naked for the camera" if slut_willingness+no_clothing_bonus >= 50 and outfit_state == 1: #If that's the only possible next step based on her outfit.
            mc.name "Let's kick it up another notch. Get completely naked for these next shots."
            call photo_naked(the_person) from _call_photo_naked_1

        "Get naked for the camera\n{menu_red}Not slutty or obedient enough{/menu_red} (disabled)" if slut_willingness+skimpy_uniform_bonus < 50 and outfit_state == 1:
            pass

        "Touch yourself" if slut_willingness+masturbate_bonus >= 60 and outfit_state == 2:
            mc.name "You're already undressed for the occasion, so lean against that wall and touch yourself for the camera. I want to see you really get into it."
            call photo_touch(the_person) from _call_photo_touch_1

        "Touch yourself\n{menu_red}Not slutty or obedient enough{/menu_red} (disabled)" if slut_willingness+masturbate_bonus < 60 and outfit_state == 2:
            pass

        "Finish the shoot":
            "You take a few final pictures."
            mc.name "I think I got everything we need. Good job [the_person.title], you look great."
            $ the_person.change_happiness(3)
            the_person "Glad to hear it, that was fun!"
            return the_person.outfit.outfit_slut_score
    return _return

label photo_flash(the_person):
    $ the_person.event_triggers_dict["camera_flash"] = True

    $ removed_something = False
    if the_person.wearing_bra or the_person.has_dress:
        if the_person.effective_sluttiness("underwear_nudity") >= 30:
            # She's slutty enough to do it.
            "[the_person.title] nods and starts to take off her [the_person.outfit.get_upper_top_layer.display_name]."

        else:
            # She's doing it for obedience or has a taboo
            "[the_person.possessive_title!c] hesitates."
            the_person "This is really what you think we need to do for the ad?"
            mc.name "Come on [the_person.title], I'm counting on you."
            "She takes a deep breath, then presses on and starts to take off her [the_person.outfit.get_upper_top_layer.display_name]."

        $ the_person.draw_animated_removal(the_person.outfit.get_upper_top_layer)
        $ removed_something = True

    if not the_person.wearing_bra:
        the_person "I can't take off my top, I'm not wearing a bra..."

    if the_person.wearing_panties:
        $ mc.change_locked_clarity(10)
        if removed_something:
            if not the_person.outfit.panties_covered:
                "When she drops it she's wearing only her underwear."
            else:
                "She pulls it off and drops it to the ground, then starts to pull off her [the_person.outfit.get_lower_top_layer.display_name]."
                $ the_person.draw_animated_removal(the_person.outfit.get_lower_top_layer)
                "When that comes off she's left wearing only her underwear."
        else:
            if the_person.effective_sluttiness("underwear_nudity") >= 30:
                "[the_person.title] nods and starts to take off her [the_person.outfit.get_lower_top_layer.display_name]."
            else:
                "[the_person.possessive_title!c] hesitates."
                the_person "This is really what you think we need to do for the ad?"
                mc.name "Come on [the_person.title], I'm counting on you."
                "She takes a deep breath, then presses on and starts to take off her [the_person.outfit.get_lower_top_layer.display_name]."

            $ the_person.draw_animated_removal(the_person.outfit.get_lower_top_layer)

    if the_person.judge_outfit(the_person.outfit):
        the_person "Time for you to get those shots [the_person.mc_title]!"
        $ mc.change_locked_clarity(10)
        $ the_person.draw_person(position = "stand3", emotion = "happy")
        "[the_person.title] gives you a few different poses in her underwear."
        $ mc.change_locked_clarity(10)
        $ the_person.draw_person(position = "stand4", emotion = "happy")

    else:
        the_person "Take those pictures before I have second thoughts..."
        $ mc.change_locked_clarity(10)
        $ the_person.draw_person(position = "stand3")
        "[the_person.title] switches quickly between a few different poses, obviously a little uncomfortable with her state of undress."
        $ the_person.draw_person(position = "stand4")

    if the_person.break_taboo("underwear_nudity"):
        "She seems to relax after her initial hesitation and becomes more comfortable in her underwear as the shoot goes on."
    $ the_person.update_outfit_taboos()


    $ slut_willingness = the_person.effective_sluttiness(["bare_tits","bare_pussy"]) + (5*the_person.opinion.not_wearing_anything)
    if the_person.obedience > 120:
        $ slut_willingness += the_person.obedience - 120
    menu:
        "Strip naked" if slut_willingness >= 50:
            mc.name "That's great [the_person.title], this is great material. Next up I want to get some nude shots, so keep stripping for me."
            call photo_naked(the_person) from _call_photo_naked_2

        "Strip naked\n{menu_red}Not slutty or obedient enough{/menu_red} (disabled)" if slut_willingness < 50:
            pass

        "Finish the shoot":
            mc.name "I think I've got all the pictures we need, we can call it there."
            the_person "Yay, glad to help!"
            $ the_person.change_stats(obedience = 2, slut = 1, max_slut = 20)
            return the_person.outfit.outfit_slut_score

    return _return

label photo_naked(the_person):
    $ the_person.event_triggers_dict["camera_naked"] = True
    if the_person.effective_sluttiness(["bare_tits","bare_pussy"]) >= 40:
        the_person "You got it [the_person.mc_title], I'm up for a little tasteful nudity."
        "You make sure to get some pictures as she strips off her underwear."
    else:
        the_person "Okay... I think I can do that..."
        "She takes a few deep breaths before she starts to take off her underwear. You make sure to get some pictures as she strips down."

    call photo_strip_naked(the_person) from _call_photo_strip_naked_2


    if the_person.judge_outfit(the_person.outfit, 5*the_person.opinion.not_wearing_anything) and not the_person.has_taboo(["bare_tits", "bare_pussy"]):
        "[the_person.title] drops her underwear to the side and turns to face you."
        the_person "There! How do I look? Good?"
        $ the_person.draw_person(position = "back_peek")
        $ mc.change_locked_clarity(15)
        "She winks at you and gives you a quick spin, showing off her ass."

    else:
        "[the_person.title] seems unsure of what to do now that she's completely naked."
        the_person "Oh my god [the_person.mc_title], my heart is pounding... I feel so vulnerable like this."
        mc.name "You look great [the_person.title], just give me a little spin and relax. Let me do all the hard work, you just have to look pretty."
        $ mc.change_locked_clarity(10)
        $ the_person.draw_person(position = "back_peek")
        if the_person.has_significant_other:
            the_person "Do.... do you think my [the_person.so_title] would be okay with this?"
            $ the_person.draw_person()
            the_person "It's not like we're doing anything wrong, this is all just for work."
            menu:
                "Reassure her":
                    mc.name "If he was a reasonable person he'd be fine with this."
                    mc.name "You're using your, uh, natural talents to perform your job as well as you can. That's an admirable thing to do."
                    $ the_person.change_stats(happiness = 2, slut = 1, max_slut = 40)
                    "She smiles and nods."
                    the_person "Yeah, that's what I think too."

                "Make her worry":
                    mc.name "I don't know [the_person.title]. Some men would be very jealous that you were showing off your body to anyone but them."
                    mc.name "Me and you both know it's for the good of the company, but he might not see it that way."
                    $ the_person.draw_person(emotion = "sad")
                    mc.name "But I wouldn't worry about it too much. We can keep it our little secret if you'd like."
                    $ the_person.change_stats(happiness = -5, obedience = 2)
                    the_person "That... might be a good idea. Thanks [the_person.mc_title]."
                    mc.name "No problem. Now smile for the camera and let me get a good look at your tits for this next shot."

        else:
            "She gives you a quick spin before turning back."

        if the_person.has_taboo(["bare_tits", "bare_pussy"]):
            "Despite her initial hesitation, [the_person.title] soon seems quite comfortable in front of the camera without her clothes on."
            $ the_person.update_outfit_taboos()


    $ the_person.draw_person()
    the_person "Do you have all the shots you want, or did you have something more in mind?"
    $ slut_willingness = the_person.effective_sluttiness()
    if the_person.obedience > 120:
        $ slut_willingness += the_person.obedience - 120
    $ slut_willingness += the_person.opinion.masturbating * 5

    menu:
        "Touch yourself" if slut_willingness >= 45:
            mc.name "I want to get some more sensual shots of you. Lean back against the wall and touch yourself."
            call photo_touch(the_person) from _call_photo_touch_2

        "Touch yourself\n{menu_red}Not slutty or obedient enough{/menu_red} (disabled)" if slut_willingness < 45:
            pass

        "Finish the shoot":
            mc.name "I think that's everything we need."
            $ the_person.change_stats(obedience = 2, slut = 2, max_slut = 50)
            "[the_person.title] collects her things and you finish up the photo shoot."
            return the_person.outfit.outfit_slut_score
    return _return

label photo_touch(the_person):
    $ the_person.event_triggers_dict["camera_touch"] = True
    if the_person.effective_sluttiness() >= 45:
        $ mc.change_locked_clarity(10)
        "[the_person.title] doesn't hesitate at all. She takes a step back and leans against the wall, spreading her legs slightly."
    else:
        the_person "Touch myself? What do you... what do you mean, [the_person.mc_title]? I couldn't... do that in front of you."
        "[the_person.title] looks nervous. She seems suddenly self-conscious, turning side-on to the camera to limit how much it can see."
        mc.name "Just relax. It's not like you haven't done it before, right?"
        the_person "Well obviously not, but..."
        mc.name "And it's not like you're the first person to touch themselves in front of a camera."
        the_person "Yeah, I know but..."
        mc.name "And it's for the business. You want us to do well, right?"
        if the_person.has_significant_other:
            the_person "But what do I tell my [the_person.so_title]? What happens if he sees our ad and sees all of this?"
            mc.name "Tell him whatever you want, he doesn't control you. The only important question is if you want to do this."
            "She thinks about it for a long moment."
            the_person "Yeah, I do. For you. Uh, I mean, for your business."
            mc.name "Then he should respect what you want to do. If he doesn't, that's his problem."
            $ the_person.change_stats(obedience = -1, slut = 1 + the_person.opinion.cheating_on_men, max_slut = 60)
            $ the_person.discover_opinion("cheating on men")
            "[the_person.possessive_title!c] seems filled with a sudden resolve. She takes a deep breath and turns back towards the camera."
            the_person "You're right. Fuck him if he isn't happy about it."
            $ mc.change_locked_clarity(10)
            "She leans back against the wall and spreads her legs slightly."

        else:
            the_person "Yeah... Of course I do. You're right."
            "She takes a deep breath shakes her arms out, like an athlete about to perform. Her cute tits jiggle as she moves."
            the_person "You can do this. Just relax [the_person.fname], you can do this."
            $ mc.change_locked_clarity(5)
            "She leans back against the wall and spreads her legs slightly."

    "[the_person.possessive_title!c] slowly runs her hand up her inner thigh. You can hear her breath catch in her throat as she comes closer to the top."
    "She stops just before she reaches her pussy and does it again, this time moving along the other thigh."
    "You take a few steps to the side to get a better angle of [the_person.title] as she sensually feels herself up."
    mc.name "That's great, now a little higher."
    "Her hand slides all the way up and her fingers glide gently over her slit."
    the_person "Ah..."
    $ mc.change_locked_clarity(10)
    $ play_moan_sound()
    "She hesitates for a second, then slips her middle finger into herself with a soft, throaty moan."
    "You take a few steps closer and take some more pictures."
    "[the_person.title]'s other hand comes up subconsciously and cradles a breast as she starts to slowly finger herself."
    "Without any prompting she starts to speed up. Her breathing gets louder and she slides a second finger inside."

    $ slut_willingness = the_person.effective_sluttiness("sucking_cock")
    if the_person.obedience > 120:
        $ slut_willingness += the_person.obedience - 120
    $ slut_willingness += the_person.opinion.giving_blowjobs * 5
    menu:
        "Suck my cock" if slut_willingness >= 55:
            mc.name "That's perfect [the_person.title]. Now just get onto your knees for me, we're going to get some hard core shots."
            call photo_blowjob(the_person) from _call_photo_blowjob_1

        "Suck my cock\n{menu_red}Not slutty or obedient enough{/menu_red} (disabled)" if slut_willingness < 55:
            pass

        "Take photos as she climaxes":
            the_person "Ah... Hah..."
            "[the_person.possessive_title!c] turns her head away from the camera and closes her eyes to focus on the task at hand."
            $ mc.change_locked_clarity(10)
            "She moves both hands down to her pussy, fingering herself with one and rubbing her clit with the other."
            the_person "Do... oh god, do you want me to go all the way?"
            mc.name "Yes, I do. I'm going to get some great pictures as it happens."
            $ play_moan_sound()
            "She moans louder and tilts her head back."
            the_person "I'm... going to cum! Fuck!"
            $ the_person.change_stats(happiness = 5, slut = 1, max_slut = 60)
            $ mc.change_locked_clarity(10)
            $ the_person.have_orgasm()
            "She gasps and tenses up, both hands moving as fast as she can make them."
            "Then the tension melts away and she slumps a little against the wall. She sighs and opens her eyes."
            the_person "Did you get that?"
            mc.name "Yeah, I got it."
            the_person "Good, I don't think I could manage that again. Whew..."
            "[the_person.title] goes to get cleaned up and you finish up the shoot."
            return the_person.outfit.outfit_slut_score + (the_person.foreplay_sex_skill * 5)
    return _return

label photo_blowjob(the_person):
    $ the_person.event_triggers_dict["camera_suck"] = True

    if the_person.effective_sluttiness("sucking_cock") >= 55 and not the_person.has_taboo("sucking_cock"):
        "You step towards her and [the_person.title] kneels down."
        the_person "Make sure I'm in focus."
        $ mc.change_locked_clarity(10)
        "She reaches for your pants and unzips your fly."

    else:
        if the_person.has_significant_other:
            the_person "Wait, wait, wait. This really crosses a line, don't you think?"
            mc.name "What do you mean?"
            the_person "I can justify doing some nude shots. I can understand wanting some sensual shots with me touching myself."
            the_person "But how could I ever tell my [the_person.so_title] about giving someone else a blowjob?"
            "She crosses her arms and looks away."
            "You lower the camera and take a step closer to [the_person.possessive_title]. You reach out and touch her shoulder. She looks up at you."
            mc.name "Don't think about your [the_person.so_title] right now. Think about me, and the business, and what you want to do."
            mc.name "We can make sure he never sees these ads. I need you, [the_person.title]."
            "Her expression softens. Finally she sighs and uncrosses her arms."
            the_person "I... I can't believe I'm going to do this. Make sure to get plenty of good shots, make this worth it."
            $ mc.change_locked_clarity(10)
            "She kneels down in front of you and unzips your fly for you."

        else:
            "She takes an unsteady step forward, then pauses."
            the_person "I don't know [the_person.mc_title]..."
            mc.name "It's for the company [the_person.title], don't let me down now."
            $ mc.change_locked_clarity(5)
            "After a moment of hesitation she comes closer and kneels down. She reaches out and undoes your fly."


    $ the_person.draw_person(position = "blowjob")
    "You hold the camera in one hand, positioning it to the side as [the_person.possessive_title] pulls your pants down."
    the_person "Let's see what I'm working with down here."
    "Your hard cock springs free of your underwear as she yanks it down."
    if the_person.effective_sluttiness("sucking_cock") >= 65 or the_person.opinion.giving_blowjobs > 0:
        the_person "Mmm, that's what I like to see."
    else:
        the_person "Sweet Jesus..."
    $ mc.change_locked_clarity(10)
    $ the_person.draw_person(position = "blowjob", special_modifier = "blowjob")
    "She licks at the tip a couple of times, then slips it into her mouth."
    $ the_person.break_taboo("sucking_cock")
    "You feel [the_person.title]'s tongue lick at the bottom of your shaft as she starts to move her head, bobbing it back and forth."
    "You try to stay focused and snap a few more pictures as she sucks you off."

    if the_person.has_significant_other:
        $ play_ring_sound()
        "Suddenly, [the_person.possessive_title]'s phone buzzes on the floor beside her."
        "She pulls back and looks over—it's [the_person.SO_name]."
        if the_person.is_affair:
            the_person "It's [the_person.SO_name]... I should probably answer."
            mc.name "Go ahead—but don't stop what you're doing."
            the_person "What? I can't talk to him while I'm—"
            mc.name "I said don't stop."
        else:
            the_person "That's my [the_person.so_title]. I have to answer, he'll know something is wrong if I don't."
            mc.name "Answer it. Just keep going."
            the_person "Are you serious right now?!"
            mc.name "It's for the business. Don't. Stop."
        "She gives you a horrified look, but reaches for the phone and accepts the call."
        the_person "[the_person.SO_name]? Hey... I'm just—uh—finishing up some... work stuff."
        "You rest a hand on the back of her head and ease her back onto your cock."
        the_person "Mm—yeah, I'm fine. Sorry, I was just... lifting something."
        "She continues bobbing her head as the conversation goes on."
        if the_person.opinion.giving_blowjobs > 0:
            "Her eyes meet yours over the phone and there's a flicker of something—excitement or shame, you can't quite tell."
        the_person "Okay... I love you too. I'll be home soon, bye."
        "She hangs up and pulls back, breathing hard."
        if the_person.has_taboo("sucking_cock"):
            the_person "I cannot believe I just did that. I feel terrible."
        else:
            the_person "That was so wrong. And I can't decide if that made it better or worse."
        $ the_person.change_stats(happiness = -3, slut = 1, max_slut = 70)
        $ mc.change_locked_clarity(10)
        if renpy.random.randint(0, 1) == 0:
            $ the_person.increase_opinion_score("cheating on men")


    $ slut_willingness = the_person.effective_sluttiness("sucking_cock")
    if the_person.obedience > 120:
        $ slut_willingness += the_person.obedience - 120
    $ slut_willingness += the_person.opinion.vaginal_sex * 5
    menu:
        "Fuck her" if the_person.effective_sluttiness("vaginal_sex") >= 65:
            mc.name "We've come this far, there's only one more thing we can do. Lie down so I can fuck you."
            $ the_person.draw_person(position = "blowjob")
            call photo_sex(the_person) from _call_photo_sex_1

        "Fuck her ass" if the_person.effective_sluttiness("anal_sex") >= 65:
            mc.name "We've come this far, let's get one more kind of shot. Turn around and stick your ass up for me."
            $ the_person.draw_person(position = "blowjob")
            call photo_anal(the_person) from _call_photo_anal_1

        "Take photos as you cum":
            mc.name "I'm going to cum, get ready!"
            $ the_person.draw_person(position = "kneeling1")
            "You pull your cock out of [the_person.possessive_title]'s mouth and stroke it off with your left hand, working the camera with your right."
            $ climax_controller = ClimaxController(["Cum on her face","face"])
            $ climax_controller.show_climax_menu()
            $ mc.change_locked_clarity(10)
            "She looks up at you as you cum, blowing your hot load over her face. You struggle to keep the camera pointed in the right direction."
            $ the_person.cum_on_face()
            $ the_person.draw_person(position = "kneeling1")
            $ climax_controller.do_clarity_release(the_person)
            "It takes you a couple long seconds to recover from your orgasm."
            $ mc.change_locked_clarity(10)
            "When you're able to you recentre the camera and take a few pictures of [the_person.title]'s cum-splattered face."
            the_person "How do I look?"
            $ the_person.draw_person(position = "kneeling1", emotion = "happy")
            mc.name "Beautiful. Smile for the camera!"
            "Once you've taken all the pictures you think you'll need you get cleaned up."
            return the_person.outfit.outfit_slut_score + 10 + (5* the_person.oral_sex_skill)
    return _return

label photo_sex(the_person):
    $ the_person.event_triggers_dict["camera_fuck"] = True

    if the_person.effective_sluttiness("vaginal_sex") < 65 or the_person.has_taboo("vaginal_sex"):
        if the_person.has_significant_other:
            if the_person.is_affair:
                the_person "That would be nice [the_person.mc_title], just make sure my [the_person.so_title] doesn't find out."
                mc.name "Don't worry, just do what feels natural."
            else:
                the_person "I can't do that [the_person.mc_title], my [the_person.so_title]..."
                mc.name "We've gone so far already, what's the difference? Just relax and do what feels natural."
                "Her resistance wavers, then melts away."
        else:
            the_person "I can't do that [the_person.mc_title]..."
            mc.name "We've gone so far already, what's the difference? Just relax and do what feels natural."
            "Her resistance wavers, then melts away."
    else:
        "[the_person.title] nods excitedly."

    if not the_person.vagina_available:
        "You tell her to take off some clothes."
        if the_person.outfit.can_half_off_to_vagina():
            $ generalised_strip_description(the_person, the_person.outfit.get_half_off_to_vagina_list(), position = "stand3", half_off_instead = True)
        else:
            $ generalised_strip_description(the_person, the_person.outfit.get_full_strip_list(), position = "stand3")

    $ the_person.draw_person(position = "missionary")
    $ mc.change_locked_clarity(10)
    "She lies down and you get on your knees. You pull her close to you, legs to either side with her pussy in line with your hard cock."
    if the_person.has_taboo("vaginal_sex"):
        $ the_person.call_dialogue("vaginal_sex_taboo_break")
        $ the_person.break_taboo("vaginal_sex")
    $ mc.condom = False #Just in case we didn't maintain it properly or something
    call condom_ask(the_person) from _call_condom_ask_2
    if not _return and not mc.condom: #We don't have an easy case to fail out to here, so we just "pretend" you have a second chance to do the right thing with some stat penalties.
        mc.name "But we need these shots [the_person.title]. The whole business is going to suffer without them."
        $ mc.change_locked_clarity(20)
        "You tap your shaft against her pussy lips, eliciting a soft whimper."
        mc.name "You don't want everyone to suffer because of you, do you? You're better than that."
        $ the_person.change_stats(happiness = -5, obedience = 2)
        the_person "I... I..."
        if the_person.wants_creampie:
            "All resistance drains from her and she starts to rub her hips against you eagerly."
            the_person "Fine, okay, you can fuck me raw. These pictures need to be perfect though, so..."
            the_person "You can even cum inside me, if you think you need to."
        else:
            "All resistance drains from her and she starts to rub her hips against you eagerly."
            the_person "Fine, okay, you can fuck me raw. Just... Be careful, okay? You need to pull out."
            the_person "The pictures will look best with me covered in cum anyways, right?"
        mc.name "You're a team player [the_person.title]. One of the best."

    $ mc.change_locked_clarity(50)
    $ the_person.increase_vaginal_sex()
    "You pull on [the_person.title]'s hips and thrust forward. Her pussy is warm and wet, inviting you in."
    $ the_person.call_dialogue("sex_responses_vaginal")
    "You thrust as best you can from a kneeling position, your hands busy with the camera."
    $ mc.change_locked_clarity(50)
    "You take pictures of [the_person.possessive_title]'s face as you fuck her and her cunt as you slide in and out."
    if the_person.has_significant_other and the_person.effective_sluttiness() > 65:
        "You hear [the_person.title] mumble to herself."
        the_person "I'm sorry, sweetheart, but this feels so good..."

    $ mc.change_locked_clarity(50)
    "You lay into her, fucking her until you feel your orgasm approaching."
    $ the_person.change_slut(2, 80)
    mc.name "Fuck, here I cum!"
    $ the_person.call_dialogue("cum_pullout")

    $ came_inside_mod = 0
    $ climax_controller = ClimaxController(["Pull out and cum", "body"],["Cum inside her", "pussy"])
    $ the_choice = climax_controller.show_climax_menu()
    if the_choice == "Pull out and cum":
        $ the_person.change_slut(the_person.opinion.being_covered_in_cum, 80)
        $ the_person.discover_opinion("being covered in cum")
        if mc.condom:
            "You pull out of [the_person.title]'s tight pussy. You whip the condom off with your left hand, then start to stroke yourself to completion."

        else:
            "You pull out of [the_person.title]'s tight pussy and grab it with your left hand, stroking yourself to completion."

        $ mc.change_locked_clarity(20)
        "You fire your load out over her, struggling to keep the camera pointed in the right direction."
        $ the_person.cum_on_stomach()
        $ the_person.draw_person(position = "missionary")
        $ climax_controller.do_clarity_release(the_person)
        "She gasps softly as she is spattered with your hot cum. For a few seconds you're both quiet as you catch your breath."

    elif the_choice == "Cum inside her":
        if mc.condom:
            "You pull on [the_person.title]'s hips one-handed and thrust as deep as you can into her."
            $ climax_controller.do_clarity_release(the_person)
            $ the_person.call_dialogue("cum_condom")
            "When you're sure you're finished you pull yourself out of [the_person.title]'s warm pussy. The condom is ballooned at the tip with your seed."
            menu:
                "Pour it on her":
                    mc.name "One last picture to get..."
                    the_person "What? What more is there to do?"
                    "You ignore her and carefully slide the cum-filled condom off of your dick into the palm of your hand."
                    mc.name "Smile for the camera."
                    "[the_person.possessive_title!c] gasps when you lean forward and dump the contents of the condom over her face."
                    $ the_person.cum_on_face()
                    $ the_person.draw_person(position = "missionary")
                    if the_person.opinion.being_covered_in_cum > 0:
                        the_person "Oh my god, ah..."
                    else:
                        the_person "Oh fuck, [the_person.mc_title], not on my face..."

                    $ mc.change_locked_clarity(10)
                    "You make sure to get some close-up shots of [the_person.title]'s face covered in your cum."
                    $ came_inside_mod = 5

                "Just get some pictures":
                    "You make sure to get some shots of [the_person.title]'s blushing face and dripping wet pussy."

        else:
            $ the_person.change_slut(1+the_person.opinion.creampies, 80)
            "You pull on [the_person.title]'s hips one-handed and thrust as deep as you can into her."
            "You stay tight against her while you pump your hot load deep inside her pussy."
            $ mc.change_locked_clarity(10)
            $ play_moan_sound()
            "She closes her eyes and moans."
            $ the_person.call_dialogue("cum_vagina")
            $ the_person.cum_in_vagina()
            $ the_person.draw_person(position = "missionary")
            $ climax_controller.do_clarity_release(the_person)
            $ came_inside_mod = 10
            "For a few seconds you're both quiet, panting for breath. You make sure to get some pictures as you pull out and your cum drips out of her cunt."



    mc.name "I think I got all the pictures I'll need."
    the_person "I would hope so. This would be a hell of a time to realise the lens cap was on."

    if the_person.has_significant_other and not the_person.is_affair:
        $ the_person.event_triggers_dict["model_ad_bf_recognise"] = day + 3

    $ mc.condom = False
    return the_person.outfit.outfit_slut_score + 15 + (5* the_person.vaginal_sex_skill) + came_inside_mod

label photo_anal(the_person):
    $ the_person.event_triggers_dict["camera_anal"] = True

    if the_person.effective_sluttiness("anal_sex") < 65 or the_person.has_taboo("anal_sex"):
        if the_person.has_significant_other:
            if the_person.is_affair:
                the_person "Sure [the_person.mc_title], just make sure my [the_person.so_title] never sees these."
                mc.name "Don't worry, just do what feels natural."
            else:
                the_person "I can't do that [the_person.mc_title], my [the_person.so_title]..."
                mc.name "We've gone so far already, what's the difference? Just relax and do what feels natural."
                "Her resistance wavers, then melts away."
        else:
            the_person "I don't know [the_person.mc_title], that's a lot to ask..."
            mc.name "We've already come this far. Just relax and let me get these shots."
            "Her resistance wavers, then melts away."
    else:
        "[the_person.title] nods."
        the_person "If it's for the shoot, then let's make it good."

    if not the_person.vagina_available:
        "You tell her to take off some clothes."
        if the_person.outfit.can_half_off_to_vagina():
            $ generalised_strip_description(the_person, the_person.outfit.get_half_off_to_vagina_list(), position = "stand3", half_off_instead = True)
        else:
            $ generalised_strip_description(the_person, the_person.outfit.get_full_strip_list(), position = "stand3")

    $ the_person.draw_person(position = "doggy")
    $ mc.change_locked_clarity(10)
    "She bends forward and braces herself against the wall. You position yourself behind her and get the camera ready."
    if the_person.has_taboo("anal_sex"):
        $ the_person.call_dialogue("anal_sex_taboo_break")
        $ the_person.break_taboo("anal_sex")
    $ mc.condom = False
    call condom_ask(the_person, "Anal") from _call_condom_ask_3
    if not _return and not mc.condom:
        mc.name "But we need these shots [the_person.title]. The whole business is going to suffer without them."
        $ mc.change_locked_clarity(20)
        "You push the tip of your cock against her ass, drawing a sharp intake of breath from [the_person.title]."
        mc.name "You don't want everyone to suffer because of you, do you? You're better than that."
        $ the_person.change_stats(happiness = -5, obedience = 2)
        the_person "I..."
        if the_person.wants_creampie:
            "All resistance drains from her and she pushes back against you slightly."
            the_person "Fine, okay. Just be gentle, alright? And you can... cum inside if you need to."
        else:
            "All resistance drains from her and she steadies herself."
            the_person "Fine, but you pull out, you hear me? The pictures will look better with you finishing on me anyway."
        mc.name "You're a real team player [the_person.title]. One of the best."

    $ mc.change_locked_clarity(50)
    $ the_person.increase_anal_sex()
    "You press forward and feel the tight resistance of [the_person.possessive_title]'s ass give way as you push inside."
    $ the_person.call_dialogue("sex_responses_anal")
    "You thrust slowly at first, camera in one hand as you hold her hip with the other."
    $ mc.change_locked_clarity(50)
    "You take pictures of [the_person.possessive_title]'s face and her ass as you slide in and out."
    if the_person.has_significant_other and the_person.effective_sluttiness() > 65:
        "You hear [the_person.title] whisper to herself."
        the_person "I'm sorry, sweetheart, but I needed this..."

    $ mc.change_locked_clarity(50)
    "You lay into her, fucking her ass until you feel your orgasm approaching."
    $ the_person.change_slut(2, 85)
    mc.name "Fuck, here I cum!"
    $ the_person.call_dialogue("cum_pullout")

    $ came_inside_mod = 0
    $ climax_controller = ClimaxController(["Pull out and cum", "body"], ["Cum inside her ass", "anal"])
    $ the_choice = climax_controller.show_climax_menu()
    if the_choice == "Pull out and cum":
        $ the_person.change_slut(the_person.opinion.being_covered_in_cum, 85)
        $ the_person.discover_opinion("being covered in cum")
        if mc.condom:
            "You pull out of [the_person.title]'s ass and whip the condom off with your left hand, then start to stroke yourself to completion."
        else:
            "You pull out of [the_person.title]'s ass and grab it with your left hand, stroking yourself to completion."

        $ mc.change_locked_clarity(20)
        "You fire your load over her, struggling to keep the camera pointed in the right direction."
        $ the_person.cum_on_stomach()
        $ the_person.draw_person(position = "doggy")
        $ climax_controller.do_clarity_release(the_person)
        "She gasps softly as she is spattered with your hot cum. For a few seconds you're both quiet as you catch your breath."

    elif the_choice == "Cum inside her ass":
        if mc.condom:
            "You pull on [the_person.title]'s hips one-handed and thrust as deep as you can into her ass."
            $ climax_controller.do_clarity_release(the_person)
            $ the_person.call_dialogue("cum_condom")
            "When you're sure you're finished you pull yourself out. The condom is filled at the tip with your seed."
            menu:
                "Pour it in her ass":
                    mc.name "One last picture to get..."
                    the_person "What? What more is there to do?"
                    "You ignore her and carefully slide the condom off and tip the contents into [the_person.possessive_title]'s ass."
                    mc.name "Smile for the camera."
                    $ the_person.cum_in_ass()
                    $ the_person.draw_person(position = "doggy")
                    if the_person.opinion.anal_creampies > 0:
                        the_person "Oh my god, ah..."
                    else:
                        the_person "What the fuck, [the_person.mc_title]..."
                    $ mc.change_locked_clarity(10)
                    "You make sure to get some close-up shots."
                    $ came_inside_mod = 5

                "Just get some pictures":
                    "You make sure to get some shots of [the_person.title]'s blushing face."

        else:
            $ the_person.change_slut(1 + the_person.opinion.anal_creampies, 85)
            "You pull on [the_person.title]'s hips one-handed and thrust as deep as you can into her ass."
            "You stay tight against her while you pump your hot load deep inside."
            $ mc.change_locked_clarity(10)
            $ play_moan_sound()
            "She groans and tenses around you."
            $ the_person.call_dialogue("cum_anal")
            $ the_person.cum_in_ass()
            $ the_person.draw_person(position = "doggy")
            $ climax_controller.do_clarity_release(the_person)
            $ came_inside_mod = 10
            "For a few seconds you're both quiet, panting for breath. You make sure to get some pictures as you pull out."

    mc.name "I think I got all the pictures I'll need."
    the_person "That was quite the photo shoot, [the_person.mc_title]."

    $ mc.condom = False
    return the_person.outfit.outfit_slut_score + 20 + (5 * the_person.anal_sex_skill) + came_inside_mod

label photo_strip_naked(the_person): #A helper label that strips a girl until her top and bottom are available for whatever you want to use them fore
    $ generalised_strip_description(the_person, the_person.outfit.get_full_strip_list(strip_feet = False))
    $ mc.change_locked_clarity(20)
    return

label model_ad_bf_recognise_label():
    $ the_person = mc.business.company_model
    if not the_person:
        return
    if not the_person.has_significant_other:
        $ the_person.event_triggers_dict.pop("model_ad_bf_recognise", None)
        return

    $ play_ring_sound()
    "You get a call from [the_person.title]."
    $ mc.start_text_convo(the_person)
    the_person "[the_person.mc_title]... I just got a call from [the_person.SO_name]."
    the_person "He saw the company ad. He recognised me."
    mc.name "What did you tell him?"
    the_person "I didn't know what to say. He's furious."
    menu:
        "Tell her to deny everything":
            mc.name "Tell him it's just someone who looks like you. A model we hired. He'll calm down."
            the_person "You think that'll work?"
            mc.name "It'll have to. Just stick to your story."
            the_person "...Okay. I'll try."
            "There's a long pause."
            the_person "Alright, I'll handle it. I don't know how, but I will."
            the_person "And... well, if I'm going to be lying to him anyway, I might as well get something out of it."
            the_person "Maybe we could... keep things going, between us. Discreetly."
            $ the_person.add_role(affair_role)
            $ the_person.increase_opinion_score("cheating on men")
            $ the_person.change_stats(happiness = -5, love = -2)

        "Tell her to come clean":
            mc.name "It might be easier to just come clean. It was for work—maybe he'll understand."
            the_person "You want me to tell my [the_person.so_title] I let my boss photograph me having sex?!"
            mc.name "When you put it that way..."
            the_person "Yeah. Exactly."
            if renpy.random.randint(0, 1) == 0:
                "She calls him back. A few minutes later you can hear raised voices through the door, then silence."
                the_person "It's over. He couldn't handle it."
                $ the_person.relationship = "Single"
                $ the_person.SO_name = None
                $ the_person.remove_role(affair_role)
            else:
                "She calls him back. There's a long, quiet conversation before she hangs up."
                the_person "He said he needs time to think. I don't know where we stand."
            $ the_person.change_stats(happiness = -10, love = -5)

        "It's not your problem":
            mc.name "This is between you and him. Sort it out."
            the_person "That's... that's all you have to say?"
            mc.name "The pictures are taken. The ad is out. Handle your personal life on your own time."
            "She ends the call. Later that day you hear that [the_person.title] and [the_person.SO_name] have split up."
            $ the_person.relationship = "Single"
            $ the_person.SO_name = None
            $ the_person.remove_role(affair_role)
            $ the_person.change_stats(happiness = -20, love = -10)
            "She's barely spoken to you since."

    $ mc.end_text_convo()
    $ the_person.event_triggers_dict.pop("model_ad_bf_recognise", None)
    return

label ad_expire(the_amount):
    $ mc.business.remove_sales_multiplier("Ad Campaign", the_amount)
    return
